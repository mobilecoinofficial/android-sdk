apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply from: 'publish.gradle'

protobuf {
    protoc {
        if (osdetector.os == "osx") {
            artifact = deps.network.protoc_osx
        } else {
            artifact = deps.network.protoc
        }
    }
    plugins {
        grpc {
            if (osdetector.os == "osx") {
                artifact = deps.network.grpc_protoc_osx
            } else {
                artifact = deps.network.grpc_protoc
            }
        }
        httpclientgen {
            // check if we are not running in the docker container
            if ("/root" != System.getProperty("user.home")) {
                path = rootProject.files('docker/tools/protoc-gen-httpclientgen/bin/protoc-gen-httpclientgen').getSingleFile()
            }
            else {
                path = "/usr/local/bin/protoc-gen-httpclientgen"
            }
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option "lite" }
            }
            if(task.getName().contains('Grpc')) {
                task.plugins {
                    grpc { option 'lite' }
                }
            }
            task.plugins {
                httpclientgen {
                    //
                }
            }
        }
    }
}

android {
    buildToolsVersion "30.0.3"
    compileSdkVersion build_versions.compile_sdk
    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions 'transport'
    productFlavors {
        http {
            dimension 'transport'
            versionNameSuffix '-http'
        }
        grpc {
            dimension 'transport'
        }
    }
}

sourceSets {
    http {
        java {
        }
    }
    grpc {
        java {
            srcDirs += 'src/grpcTest'
        }
    }
}

task printSourceSetInfo(){
    doLast{
        sourceSets.each { srcSet ->
            println "[" + srcSet.name + "]"
            print "src: " + srcSet.allJava.srcDirs + "\n"
            print "output: " + srcSet.output.classesDirs.files + "\n"
            srcSet.allJava.each {f ->
                println "\tFile: " + f.getName()
            }
        }
    }
}

configurations.implementation.setCanBeResolved(true)
configurations.grpcImplementation.setCanBeResolved(true)

// Generate javadoc for all source sets and generated sources
task javadoc(type: Javadoc, dependsOn: build) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    source += android.sourceSets.grpc.java.sourceFiles
    source += fileTree(dir: 'build/generated/source', include: '**/*.java')
    source += fileTree(dir: 'src/grpc', include: '**/*.java')
    source += fileTree(dir: 'src/http', include: '**/*.java')
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.implementation
    classpath += configurations.grpcImplementation
}
// Build javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

// Build sources jar
task sourceJar(type: Jar, dependsOn: build) {
    from android.sourceSets.main.java.srcDirs +
            fileTree(dir: 'src/grpc', include: '**/*.java')
    archiveClassifier.set('sources')
}

publish.dependsOn(javadocJar, sourceJar)

publish.mustRunAfter(javadocJar)
publish.mustRunAfter(sourceJar)

dependencies {
    // multidex to avoid 64K reference limit
    implementation deps.android.multidex
    // @Nullable, @NonNull annotations
    implementation deps.android.annotation
    implementation deps.android.java_annotation
    // Desugar enables to use Java 8 features without requiring a minimum API level
    // it works by using bytecode transformations during D8/R8 stage (dex + compression)
    coreLibraryDesugaring deps.android.desugar

    // Network
    grpcImplementation deps.network.grpc_okhttp
    grpcImplementation deps.network.grpc_stuf
    grpcImplementation deps.network.grpc_protobuf
    implementation deps.network.protobuf

    // JNI
    implementation deps.jni.mobilecoin

    // Testing
    testImplementation deps.testing.junit
    testImplementation deps.testing.robolectric
    testImplementation deps.testing.mockito_core
    androidTestImplementation deps.testing.mockito_android
    androidTestImplementation deps.testing.core
    androidTestImplementation deps.testing.ext_junit
    androidTestImplementation deps.testing.runner
    androidTestImplementation deps.testing.rules
    androidTestImplementation deps.testing.espresso_core
    androidTestImplementation deps.testing.support_rules
    androidTestImplementation deps.testing.okhttp3

}
